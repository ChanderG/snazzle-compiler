%{
#include <cstdio>
#include <iostream>
using namespace std;

// stuff from flex that bison needs to know about:
extern "C" int yylex();
extern "C" int yyparse();
extern "C" FILE *yyin;

void yyerror(const char *s);
%}

// Bison fundamentally works by asking flex to get the next token, which it
// returns as an object of type "yystype".  But tokens could be of any
// arbitrary data type!  So we deal with that in Bison by defining a C union
// holding each of the types of tokens that Flex could return, and have Bison
// use that union instead of "int" for the definition of "yystype":
%union {
	int ival;
	float fval;
	char *sval;
}

//constant-string tokens
%token  SNAZZLE TYPE END

// define the "terminal symbol" token types I'm going to use (in CAPS
// by convention), and associate each with a field of the union:
%token <ival> INT
%token <fval> FLOAT
%token <sval> STRING

%%
//all rules are left recursive - Bison does not perform the action until it matches the complete rule

// the first rule defined is the highest-level rule, which in our
// case is just the concept of a whole "snazzle file":
snazzle:
	header template body_section footer { cout << "done with a snazzle file!" << endl; }
	;
	header:
		SNAZZLE FLOAT { cout << "reading a snazzle file version " << $2 << endl; }
	;
	template:
		typelines
	;
	typelines:
		typelines typeline
		| typeline
	;
	typeline:
		TYPE STRING { cout << "new defined snazzle type: " << $2 << endl; }
	;
	body_section:
		body_lines
	;
	body_lines:
		body_lines body_line
		| body_line
	;
	body_line:
		INT INT INT INT STRING { cout << "new snazzle: " << $1 << $2 << $3 << $4 << $5 << endl; }
	;
	footer:
		END
	;

%%

main(int argc, char *argv[]) {

if(argc != 2) {
	cout << "Snazzle compiler usuage: " << endl << "  snazzle <name of snazzle file>" << endl;
  exit(0);
}
// open a file handle to a particular file:
FILE *myfile = fopen(argv[1], "r");
// make sure it is valid:
if (!myfile) {
	cout << "I can't open " << argv[1] << endl;
	return -1;
}
// set flex to read from it instead of defaulting to STDIN:
yyin = myfile;

// parse through the input until there is no more:
do {
	yyparse();
} while (!feof(yyin));

}

void yyerror(const char *s) {
cout << "EEK, parse error!  Message: " << s << endl;
// might as well halt now:
exit(-1);
}
